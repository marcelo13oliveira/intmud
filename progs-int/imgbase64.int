telatxt = 1
err = 2
exec = 1000000

classe x
telatxt tela
textotxt lista # Lista de arquivos
textopos plista # Arquivo atual da lista
const msg = tela.msg(arg0 + "\n")
const iniclasse = criar(arg0)

func ini
  msg("\cBPrograma ImgBase64\b")
  msg("\cEGera o código da imagem ou som para ser colocado em páginas HTTP\b")
# Lê arquivos
  arqdir d
  epara d.abrir("."), d.lin, d.depois
    continuar d.tipo != "A"
    casovar txtmin(txtfim(d.texto, 4))
    casose ".gif"
    casose ".jpg"
    casose ".png"
    casose ".mp3"
    casose ".wav"
    casose ".ogg"
      lista.addfim(d.texto)
    casofim
  efim
  lista.ordena
  plista = lista.ini
# Mensagens
  se lista.linhas == 0
    msg("Não foi encontrado nenhum arquivo GIF, JPG ou PNG no diretório")
    msg("Tecla ESC encerra o programa")
  senao
    msg("1. Escolha o arquivo com as setas para cima e para baixo")
    msg("2. Pressione ENTER")
    msg("3. Tecla ESC encerra o programa")
    msg("")
    msg("Arquivo " + plista.texto)
  fimse

func tela_msg
# Lê conteúdo do arquivo
  arqtxt arq
  arqtxt dest
  txt100 lin
  int8 blocoini
  se lista.linhas == 0
    ret msg("Não foi encontrado nenhum arquivo")
  senao !arq.abrir(plista.texto, 4)
    ret msg("Não foi possível abrir o arquivo " + plista.texto)
  senao !dest.abrir(plista.texto + ".txt", 2)
    ret msg("Não foi possível criar o arquivo " + plista.texto + ".txt")
  fimse
  lin = "<img src=\"data:image/"
  blocoini = 24
  casovar txtmin(txtfim(plista.texto, 4))
  casose ".mp3"
    lin = "<source type=\"audio/mpeg\" src=\"data:audio/"
    blocoini = 9
    sair
  casose ".wav"
    lin = "<source type=\"audio/wav\" src=\"data:audio/"
    blocoini = 9
    sair
  casose ".ogg"
    lin = "<source type=\"audio/ogg\" src=\"data:audio/"
    blocoini = 9
    sair
  casofim
  lin += txtmin(txtfim(plista.texto, 3)) + ";base64,"
  dest.msg(lin + txtconv(arq.ler(blocoini), "hb"))
  enquanto (lin = arq.ler(48)) != ""
    dest.msg("\n" + txtconv(lin, "hb"))
  efim
  dest.msg("\">\n")
  msg("\c3Criado arquivo " + plista.texto + ".txt\b")

func tela_tecla
  casovar arg0
  casose "ESC"
    terminar
  casose "UP"
    ret lista.linhas == 0, nulo
    plista.antes
    msg("Arquivo " + plista.texto)
    ret 1
  casose "DOWN"
    ret lista.linhas == 0, nulo
    plista.depois(2)
    plista.antes
    msg("Arquivo " + plista.texto)
    ret 1
  casofim
