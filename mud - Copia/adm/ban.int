classe adm_ban
herda comando_adm
const objcmd = arg1.c_ban ? este
const texto = $miscban1.texto
const salvar = $miscban1.salvar
const comando = "ban"
const txtajuda = "\c3Banir jogadores por IP\b\n\
\c2BAN\b\n\
  Mostra a lista de endereços banidos.\n\
\c2BAN\b <endereço IP> <tempo>\n\
  Impede que os jogadores de um endereço IP criem personagens no jogo.\n\
  Tempo é um número, em minutos. Se for 0, bane por tempo indeterminado.\n\
  Para banir uma faixa de endereços, terminar o endereço com um ponto.\n\
  Para banir todos os usuários novos, colocar um ponto no lugar do endereço.\n\
\c2BAN\b <endereço IP>\n\
  Desbane os jogadores do endereço especificado.\n\
\c2BAN\b <apelido>\n\
  Expulsa do MUD o jogador com o apelido especificado.\n\
\c2Exemplos:\b\n\
  ban 10.20.30.40 0  Bane 10.20.30.40 por tempo indeterminado\n\
  ban 1.2.3.4 10     Bane 1.2.3.4 por 10 minutos\n\
  ban 1.2. 0         Bane todos os endereços que começam com 1.2.\n\
  ban 1.2.3.4        Desbane 1.2.3.4\n\
  ban . 0            Bane todos; quem não tem personagem salvo não entra\n\
  ban fulano         Expulsa o jogador fulano\n\
\c2Nota:\b\n\
  Não é possível banir jogadores conectados via bot, quando o bot chat não\n\
  informa os endereços IP dos jogadores."

func escr
  textopos pos
# Nenhum argumento: lista de endereços banidos
  se !arg1
    refvar txtmais = arg0.sock.txtmais
    txtmais.limpar
    datahora dh
    dh.agora
    refvar tini = dh.numtotal
    epara pos = texto.ini, pos, pos.depois
      refvar tempo = intpos(txt2(pos.texto)) - tini
      se tempo < 0
        txtmais.addfim(txt1(pos.texto))
      senao tempo < 60
        txtmais.addfim(txt1(pos.texto) + " " + txtnum(tempo, "0") + " seg.")
      senao
        txtmais.addfim(txt1(pos.texto) + " " + txtnum(tempo / 60, "1") + " min.")
      fimse
    efim
    se !txtmais.linhas
      txtmais.addfim("Nenhum endereço banido.")
    senao
      txtmais.addini("\b\c6Endereços banidos:\b")
    fimse
    ret arg0.sock.txtmostra
# Pelo menos um argumento: checa se é endereço IP
  senao
    socket s
    refvar ender = txt1(arg1)
    se ender != "." && !s.ipvalido(ender) && !s.ipvalido(ender + "0")
      indiceitem item
      item.ini("un " + txts(txtnome(arg1)))
      refvar obj = item.obj
      ret !obj, arg0.msg("Personagem ou endereço IP inválido: " + arg1)
      obj.nomefim = ""
      apagar(obj.perso)
      apagar(obj)
      arg0.msg("Jogador expulso: " + obj.nome)
      cmdlog(arg0, comando, "expulsou " + obj.nome + " " + item.obj.info)
      obj.msg("Você foi expulso")
      ret
    fimse
  fimse
# Um argumento: desbane
  se !txt2(arg1)
    pos = texto.ini
    se pos.txtproc("\n" + arg1 + " ") < 0
      arg0.msg("Endereço não está banido: " + arg1)
    senao
      pos.remove
      salvar
      arg0.msg("Endereço foi desbanido: " + arg1)
      cmdlog(arg0, comando, "desbaniu IP " + arg1)
    fimse
    ret
# Mais de um argumento: bane
  senao
    refvar tempo = intpos(txt2(arg1))
    pos = texto.ini
    pos.txtproc("\n" + txt1(arg1) + " ") >= 0 && pos.remove
    se tempo > 0
      datahora hora
      hora.agora
      texto.addini(txt1(arg1) + " " + (tempo * 60 + hora.numtotal))
      arg0.msg("Endereço foi banido: " + txt1(arg1) + " por " + tempo + " min")
      cmdlog(arg0, comando, "baniu IP " + txt1(arg1) + " por " + tempo + " min")
    senao
      texto.addini(txt1(arg1) + " 0")
      arg0.msg("Endereço foi banido: " + txt1(arg1))
      cmdlog(arg0, comando, "baniu IP " + txt1(arg1))
    fimse
    salvar
  fimse


classe adm_ban2
herda adm_ban
const objcmd = arg0.jogperm == "admin" ? este
const texto = $miscban2.texto
const salvar = $miscban2.salvar
const comando = "ban2"
const txtajuda = "\c3Banir jogadores por IP\b\n\
Funciona exatamente como o comando Ban, porém não permite que os\n\
usuários banidos entrem no chat, mesmo com personagens salvos."
