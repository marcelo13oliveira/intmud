classe adm_arqarea
herda comando_adm, comando_editor

func escr
  se !arg1
    arg0.msg("Tecle o nome do arquivo após earq.")
  senao !arg0.sock.teditor.ler("areas/" + arg1 + ".int")
    arg0.msg("arquivo não existe: " + arg1)
  senao
    arg0.sock.tpasso = arg1
    passo(arg0.sock, "\b")
  fimse

func passo
  casovar txtmin(txt(arg1, 0, 1))
  casose "\b"
    arg0.teditor.bytes == 1 && arg0.teditor.limpar
    arg0.leditor = arg0.teditor.ini
    arg0.leditor1 = arg0.teditor.ini
    arg0.leditor2 = arg0.teditor.fim
    arg0.leditor2.antes
    arg0.passo = este
    arg0.msg("Editando arquivo " + arg0.tpasso + ", O opções, X salvar, Z sair")
    ret 1
  casose "z" # Sair do editor
    arg0.msg("Fechando editor")
    arg0.passo = ""
    ret 1
  casose "x" # Salvar o texto
    se arg0.teditor.salvar("areas/" + arg0.tpasso + ".int")
      arg0.msg("arquivo editado: " + arg0.tpasso)
    senao
      arg0.msg("Erro ao editar o arquivo: " + arg0.tpasso)
    fimse
    cmdlog(arg0, "esav", "Editou arquivo " + arg0.tpasso)
    ret 1
  casofim
  ret passoeditor(arg0, arg1, 1000000, "arquivo editado " + arg0.tpasso)
