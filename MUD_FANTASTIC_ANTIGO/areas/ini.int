classe a_ini
herda area
const a_nome = "Ilha (início)"


classe b_ini
herda sala
const s_area = "ini"
const s_terreno = 1
const s_luta = 1
const s_item = 1


classe i_ini001
herda comum_itemgrupo
const nome = "capacete"
const ident = "capacete"
const descnome = "um capacete"
const msexo = 1 # Sexo Msg: 1 Masculino
const vestir1 = 4 # Aonde vestir: 2-na cabeça

func recalc2_auto # Altera atributos do personagem
# Não alterar; função criada automaticamente
  pvidamax += 1 * pnivel + 2
  pmanamax += 1 * pnivel + 2
  pmovemax += 1 * pnivel + 2
  pveloc1 += 1 * pnivel + 2
  atknorm += 1 * pnivel + 2
  defnorm += 1 * pnivel + 2
  atkesp += 1 * pnivel + 2
  defesp += 1 * pnivel + 2


classe i_ini002
herda comum_itemgrupo
const nome = "minúscula argola"
const descnome = "uma minúscula argola"
const msexo = 1 # Sexo Msg: 1 Masculino
const vestir1 = 384 # Aonde vestir: 7-na orelha direita, 8-na orelha esquerda

func recalc2_auto # Altera atributos do personagem
# Não alterar; função criada automaticamente
  pvidamax += 1 * pnivel + 8
  pmanamax += 1 * pnivel + 2
  pmovemax += 1 * pnivel + 2
  pveloc1 += 1 * pnivel + 2
  atknorm += 1 * pnivel + 2
  defnorm += 1 * pnivel + 2
  atkesp += 1 * pnivel + 2
  defesp += 1 * pnivel + 2


classe i_ini003
herda comum_itemgrupo
const nome = "cordão"
const ident = "cordão folheado prata"
const descnome = "um cordão folheado em prata"
const vestir1 = 512 # Aonde vestir: 9-no pescoço

func recalc2_auto # Altera atributos do personagem
# Não alterar; função criada automaticamente
  pvidamax += 1 * pnivel + 2
  pmanamax += 1 * pnivel + 2
  pmovemax += 1 * pnivel + 2
  atkesp += 10


classe i_ini004
herda comum_itemgrupo
const nome = "braçadeira de couro"
const descnome = "uma braçadeira de couro"
const valor = 50
const vestir1 = 49152 # Aonde vestir: 14-no braço direito, 15-no braço esquerdo

func recalc2_auto # Altera atributos do personagem
# Não alterar; função criada automaticamente
  atknorm += 5
  atkesp += 5


classe i_ini005
herda comum_itemgrupo
const nome = "bracelete comum"
const descnome = "um bracelete comum"
const msexo = 1 # Sexo Msg: 1 Masculino
const vestir1 = 196608 # Aonde vestir: 16-no pulso direito, 17-no pulso esquerdo

func recalc2_auto # Altera atributos do personagem
# Não alterar; função criada automaticamente
  pvidamax += 20
  pmanamax += 25
  atkesp += 5


classe i_ini006
herda comum_itemgrupo
const nome = "luva de couro"
const descnome = "uma luva de couro"
const vestir1 = 786432 # Aonde vestir: 18-na mão direita, 19-na mão esquerda

func recalc2_auto # Altera atributos do personagem
# Não alterar; função criada automaticamente
  atknorm += 3
  defnorm += 3
  precisao += 5


classe i_ini007
herda comum_itemgrupo
const nome = "escudo de madeira"
const descnome = "um escudo de madeira"
const msexo = 1 # Sexo Msg: 1 Masculino
const vestir1 = 1 # Aonde vestir: 0-como escudo

func recalc2_auto # Altera atributos do personagem
# Não alterar; função criada automaticamente
  atknorm += 4
  defnorm += 4


classe i_ini008
herda comum_itemgrupo
const nome = "anel de vidro"
const descnome = "um anel de vidro"
const vestir1 = 6291456 # Aonde vestir: 21-no dedo direito, 22-no dedo esquerdo

func recalc2_auto # Altera atributos do personagem
# Não alterar; função criada automaticamente
  precisao += 15


classe i_ini009
herda comum_itemgrupo
const nome = "colete"
const descnome = "um colete"
const msexo = 1 # Sexo Msg: 1 Masculino
const vestir1 = 4096 # Aonde vestir: 12-no corpo

func recalc2_auto # Altera atributos do personagem
# Não alterar; função criada automaticamente
  deftipo.0 *= 0.99 # Tipos Normais
  deftipo.1 *= 0.99 # Tipos Especiais


classe i_ini010
herda comum_itemgrupo
const nome = "perneiras de couro"
const descnome = "um par de perneiras de couro"
const msexo = 1 # Sexo Msg: 1 Masculino
const vestir1 = 16777216 # Aonde vestir: 24-nas pernas

func recalc2_auto # Altera atributos do personagem
# Não alterar; função criada automaticamente
  pveloc1 += 20
  atkesp += 2 * pnivel + 30


classe i_ini011
herda comum_itemgrupo
const nome = "botas de couro"
const descnome = "um par de botas de couro"
const vestir1 = 134217728 # Aonde vestir: 27-nos pés

func recalc2_auto # Altera atributos do personagem
# Não alterar; função criada automaticamente
  pveloc1 += 20
  atkesp += 15
  defesp += 15


classe i_ini012
herda comum_itemgrupo
const nome = "tornozeleira"
const descnome = "uma tornozeleira"
const vestir1 = 100663296 # Aonde vestir: 25-no tornozelo direito, 26-no tornozelo esquerdo

func recalc2_auto # Altera atributos do personagem
# Não alterar; função criada automaticamente
  atktipo.1 *= 1.01 # Tipos Especiais


classe i_ini013
herda comum_itemgrupo


classe i_ini014
herda comum_itemgrupo


classe i_ini015
herda comum_itemgrupo


classe i_ini016
herda comum_itemgrupo


classe i_ini017
herda comum_itemgrupo


classe i_ini018
herda comum_itemgrupo


classe i_ini019
herda comum_itemgrupo


classe i_ini020
herda comum_itemgrupo
const nome = "curta adaga"
const descnome = "uma curta adaga"
const armamax = 70
const armamin = 70
const armatipo = 3 # Tipo de arma: 3 Lâmina curta
const valor = 200


classe i_ini021
herda comum_itemgrupo
const nome = "pequeno cajado"
const descnome = "um pequeno cajado retorcido"
const msexo = 1 # Sexo Msg: 1 Masculino
const nivel = 0
const armamax = 70
const armamin = 70
const armatipo = 2 # Tipo de arma: 2 Cajado
const valor = 200


classe p_ini001
herda comum_perso
const nome = "anão"
const descnome = "um pequeno anão"
const sexoini = 1
const pesoobj = 70000
const p_agressivo = 1 # Para poder receber ataques (não participará de batalha)
const eveste = 1 # Receber eventos do próprio objeto
const evsala = 1 # Receber eventos do dono se o dono for sala
const persobat = este # Personagem usado na batalha

func cmd_atacar
  se "p_ini001" == arg1
    arg2.result = 0
    $mens.p(arg0, arg1)
    $mens.mtodos1(arg2.mensgolpe(arg0, arg1))
    arg0.msg("Anão: Tudo bem, você venceu, pode passar.")
    arg0.var.j_atacou_anao = 1
    ret 1
  fimse

func cmd_mover
  se arg2 == "s" && arg0.jog && !arg0.var.j_atacou_anao
    arg0.msg("Anão: Se quiser passar por mim terá de me pagar 10 galões de ouro.")
    arg0.msg("Aí eu vou tirar na sorte se deixo ou não você passar.")
    arg0.msg("Você não me mete medo. Eu sei brigar muito bem.")
    ret 1
  fimse


classe p_ini002
herda comum_perso
const nome = "Silva"
const sexoini = 1
const p_agressivo = 1 # Para poder receber ataques (não participará de batalha)
const pvida = 1000 # Para não dizer que o alvo está desmaiado
const pesoobj = 80000
const eveste = 1 # Receber eventos do próprio objeto
const evsala = 1 # Receber eventos do dono se o dono for sala

func cmd_chegou # Alguém chegou na mesma sala
  ret !arg0.jog, nulo # Retorna se não for jogador
  misc:tempo("entrou", arg0) # Dispara funções 'entrou'

func entrou10
  arg0.msg("Silva: Oi! Sou o Sr. Silva. Moro nessa ilha.")

func entrou30
  se !arg0.var.h_chutar
    arg0.msg("Silva: Se vai sair da ilha, você precisa aprender alguns golpes.")
  senao
    arg0.msg("Silva: Já te ensinei o que eu sei sobre combates.")
  fimse

func entrou50
  se !arg0.var.h_chutar
    arg0.msg("Silva: Precisa saber se defender.")
    $mens.p(arg0)
    $mens.mvis1("Silva ensina $p a socar.")
    arg0.var.h_socar = 3
  fimse

func entrou70
  se !arg0.var.h_chutar
    arg0.msg("Silva: Agora tente me socar. Tecle socar Silva")
  fimse

func cmd_atacar
  se ![arg2](arg0, arg1) # Chama a função que trata do ataque específico
    arg0.msg("Silva bloqueia seu golpe.")
  fimse
  ret 1

func cmd_socar
  se arg1 != este
    arg0.msg("Silva: Tecle socar silva")
    ret 1
  senao
    $mens.p(arg0)
    $mens.mvis1("$P soca Silva, -2")
    msg("Silva: Nada mal para quem está começando!")
    msg("Silva: Agora vou te ensinar um golpe um pouco mais forte.")
    $mens.mvis1("Silva ensina $P a chutar.")
    arg0.var.h_chutar = 3
    arg0.msg("Silva: Tecle chutar silva ou, se estiver com preguiça, tecle ch silva.")
    ret 1
  fimse

func cmd_chutar
  se arg1 != este
    arg0.msg("Silva: Tecle chutar silva")
    ret 1
  senao
    $mens.p(arg0)
    $mens.mvis1("$P chuta Silva, -3")
    arg0.msg("Silva: Bom chute. Eu só tive 3 pontos de danos porque sou mais forte.")
    arg0.msg("Silva: Durante uma briga, você pode teclar apenas chutar ou socar.")
    arg0.msg("Silva: Te dou dois conselhos: Cheque suas habilidades teclando hab")
    arg0.msg("Silva: Cheque também sua vida teclando vida. Se chegar a 0 você morre.")
    ret 1
  fimse


classe p_ini003
herda comum_perso
const nome = "Joaquim"
const sexoini = 1
const pesoobj = 80000
const evsala = 1 # Receber eventos do dono se o dono for sala

func cmd_chegou # Chegou na sala
  ret !arg0.jog, nulo # Retorna se não for jogador
  misc:evento(arg0) # Receber eventos do jogador
  se arg0.dentro1.objini("i_ini002") # Se tem diamante amarelo
    arg0.var.d_resposta_ = 20 # Já fez todas as perguntas
  senao
    misc:tempo("entrou", arg0) # Se não tem, dispara funções 'entrou'
  fimse

func entrou10
  arg0.msg("Joaquim: Olá. Não quer se sentar um pouco para conversarmos?")
  arg0.msg("O comando para sentar-se é SENTAR")

func cmd_sentouchao
  arg0.msg("Joaquim: No chão não! Sente-se nesse tronco de árvore.")
  arg0.msg("Primeiro tecle LEVANTAR para ficar em pé.")
  arg0.var.d_sentouchao_ = 1

func cmd_levantou
  se arg0.var.d_sentouchao_ == 1
    arg0.msg("Joaquim: Isso. Agora sente-se no tronco. Tecle SENTAR TRONCO")
    arg0.var.d_sentouchao_ = 0
  fimse

func cmd_descansouchao
  arg0.msg("Joaquim: Descansar no tronco é muito melhor.")

func cmd_descansouobj
  arg0.msg("Joaquim: Vejo que prefere descansar. Então não vou incomodar.")

func cmd_sentouobj
  se arg0.var.d_resposta_ == 0
    arg0.var.d_resposta_ = 1
    misc:tempo("pergunta", arg0) # Dispara funções 'pergunta'
  fimse

func pergunta10
  arg0.msg("Joaquim: Esse arquipélago já foi maior. Agora pouca gente vem aqui.")

func pergunta30
  arg0.msg("Joaquim: Sou um estudioso e curioso. Você também gosta de estudar?")

func pergunta55
  arg0.msg("Joaquim: Você parece saber das coisas. Então vou fazer umas perguntas.")

func pergunta80
  arg0.var.d_resposta_ = 2
  arg0.msg("Joaquim: Tem um lugar nessas ilhas que tem muitos diamantes vermelhos.")
  arg0.msg("Quantos tem nesse lugar? Tecle falar seguido de um número de 1 a 100.")

func cmd_falou
  casovar arg0.var.d_resposta_
  casose "0" # 0 = ainda não está fazendo perguntas
  casose "1" # 1 = está instruindo o jogador
    ret
  casose "2"
    arg1 != "78" && (arg0.var.d_resposta_ = 12)
  casose "12"
    arg0.var.d_resposta_++
    arg0.msg("Joaquim: Qual o nível do meu amigo Silva? É outro número de 1 a 100.")
    ret
  casose "3"
    arg1 != "27" && (arg0.var.d_resposta_ = 13)
  casose "13"
    arg0.var.d_resposta_++
    arg0.msg("Joaquim: Quando você chegou na ilha, que habilidade você já conhecia?")
    ret
  casose "4"
    se arg1 == "atirar" # Acertou todas as perguntas
      arg0.msg("Joaquim: Você acertou as perguntas. Como recompensa fique com este diamante.")
      arg0.msg("Joaquim: Quando tiver os quatro diamantes vá para o centro da ilha maior.")
      $mens.p(este, arg0)
      $mens.mvis1("$P dá diamante amarelo para $A.")
      criar("i_ini002", arg0)
      sair
    fimse
  casose "14"
    arg0.msg("Joaquim: Você errou pelo menos uma das perguntas que eu fiz.")
    arg0.msg("Volte aqui quando souber todas as respostas.")
    misc:tempo("errou", arg0) # Dispara funções 'errou'
  casofim
  arg0.var.d_resposta_ = 20 # Não está mais fazendo perguntas

func errou80
  arg0.msg("Joaquim: O que está esperando? Vá atrás das respostas.")


classe s_ini001
herda b_ini
const s_titulo = "entrando no Killer mud, o mundo assassino!"
const s_desc = "Siga para leste e inicie sua aventura por nosso mundo!"
const s_luta = 1 # Batalha: 1 Exceto entre jogadores
const s_ocultar = 0 # Ocultar desc: Não
const eveste = 1 # Eventos sala: Sim
const dir_l = $s_midgaard001
const dir_o = $s_midgaard001

func cmd_salaperso # Personagem chegou
  ret !arg0.jog, nulo # Retorna se não for jogador
  arg0.var.h_lâmina_longa = 4
  arg0.var.h_cajado = 4

func cmd_mover
  se arg2 == "l"
    arg0.msg("Para ir de um lugar a outro tecle n, s, l, o, c, b\b")
    ref r
    se arg0.tipo2 == "ladrão"
      r = criar("i_ini020", arg0)
    senao arg0.tipo2 == "mago" || arg0.tipo2 == "druida" || arg0.tipo2 == "clérigo"
      r = criar("i_ini021", arg0)
    senao
      r = criar("i_teste012", arg0)
    fimse
    r.vestir(50)
    arg0.var.h_lâmina_longa = 8
    arg0.var.h_lãmina_curta = 8
    arg0.recalc = 1
  fimse


classe s_ini002
herda b_ini
const s_titulo = "Canais de comunicação."
const s_desc = "Os principais comandos para se comunicar no jogo são os seguintes:\n\
falar seguido da mensagem - envia uma mensagem para todos que estão na mesma sala.\n\
chat e a mensagem - envia uma mensagem para todo o mud"
const s_luta = 0
const dir_n = $s_ini010
const dir_s = $s_ini001
const dir_l = $s_ini005
const dir_o = $s_ini007

func cmd_salaperso # Personagem chegou
  ret !arg0.jog, nulo # Retorna se não for jogador
  misc:tempo("entrou", arg0) # Dispara funções 'entrou'

func entrou20
  se !arg0.dentro1.objini("i_ini001")
  senao !arg0.dentro1.objini("i_ini002")
  senao !arg0.dentro1.objini("i_ini003")
  senao !arg0.dentro1.objini("i_ini004")
  senao
    arg0.msg("Os diamantes estão se fundindo")
    $mens.p(arg0)
    $mens.mvis2("", "$P sumiu da sua vista.")
    arg0.mudadono($s_ini003)
  fimse


classe s_ini003
herda b_ini
const descsala = "Nas nuvens"
const s_ocultar = 1

func cmd_salaperso # Personagem chegou
  ret !arg0.jog, nulo # Retorna se não for jogador
  misc:tempo("entrou", arg0) # Dispara funções 'entrou'

func entrou20
  apagar(arg0.dentro1.objini("i_ini001"))
  apagar(arg0.dentro1.objini("i_ini002"))
  apagar(arg0.dentro1.objini("i_ini003"))
  apagar(arg0.dentro1.objini("i_ini004"))
  arg0.msg("Os diamantes se fundiram e você está saindo da ilha.")

func entrou50
  arg0.msg("A ilha desaparece completamente da sua vista.")

func entrou80
  arg0.msg("De repente, você acorda. Foi tudo um sonho?")

func entrou110
  arg0.pnivel = 1
  arg0.recalc = 1
  arg0.mudadono($s_casa001)
  $mens.p(arg0)
  $mens.mvis2("", "$P chegou")


classe s_ini004
herda b_ini
# Altera a descrição da sala
const s_titulo = "Na névoa branca"
const descsala = "Você está caindo!"
const s_ocultar = 1

func cmd_salaperso # Personagem chegou
  se !arg0.jog # Apaga se não for jogador
    apagar(arg0)
  senao
    arg0.msg("Você está um pouco acima da névoa branca.")
    misc:tempo("entrou", arg0) # Dispara funções 'entrou'
  fimse

func entrou30
  arg0.msg("Tenta andar mas não consegue sair do lugar.")

func entrou60
  arg0.msg("De repente você começa a descer, cada vez mais rápido.")

func entrou90
  arg0.msg("Não consegue ver mais nada por causa da névoa branca.")

func entrou120
  arg0.msg("A névoa está se dissipando, mas você continua acelerando para baixo.")

func entrou150
  arg0.msg("Você olha para cima e só vê a névoa.\nEm baixo, casas e outras coisas muito pequenas.\
")

func entrou190
  arg0.msg("As casas estão ficando cada vez maiores.")

func entrou220
  arg0.msg("Olha para cima e vê pequenas nuvens no céu.")

func entrou250
  arg0.msg("Você se espatifou no chão, está morto.")

func entrou280
  se arg0.dentro2.objini("i_ini005")
    arg0.msg("Da próxima vez que estiver caindo, tecle PUXAR para puxar a corda")
    arg0.msg("que aciona o foguete.")
  fimse

func entrou300
  arg0.mudadono($s_ini002)
  $mens.p(arg0)
  $mens.mtodos2("", "$P chegou.")
  ret 1


classe s_ini005
herda b_ini
const s_titulo = "Região leste da ilha"
const s_desc = "Não há nada aqui, somente o chão de terra que se estende à oeste.\n\
Nas outras direções o chão termina e começa uma neblina branca.\n\
Acima da neblina só se vê o céu azul. O chão nessa região também é azul,\n\
mas há um [desenho] apagado (você pode ver teclando olhar desenho ou\n\
teclando ver desenho)."
const dir_o = $s_ini002
const dir_n = $s_ini004
const dir_s = $s_ini004
const dir_l = $s_ini004
const dir_b = $s_ini006
const inv_b = 1
const ver_desenho = "O desenho é o mapa de uma pequena ilha, com uma palmeira seca no meio,\n\
um arbusto e uma escada para baixo no norte, uma cabana ao sul e uma\n\
placa à oeste. Curiosamenente, há uma escada para baixo colada na borda\n\
leste da ilha, praticamente na altura do chão."


classe s_ini006
herda b_ini
const s_titulo = "Escada"
const s_desc = "A escada está bem firme, presa na rocha à leste. Mas infelizmente,\n\
é menor que parecia à primeira vista. Depois de 3 metros, a escada\n\
simplesmente acaba no nada. O vento aqui é um pouco forte, e não seria\n\
surpresa se alguma coisa caísse de cima."
const dir_c = $s_ini005
const eveste = 1 # Receber eventos do próprio objeto

func cmd_salaperso # Personagem chegou
  ret !arg0.jog, nulo # Retorna se não for jogador
  misc:tempo("entrou", arg0) # Dispara funções 'entrou'

func entrou100
  $mens.p(arg0)
  $mens.mtodos1("Você vê um diamante azul caindo.")
  criar("i_ini001", este)

func entrou130
  se dentro1.objini("i_ini001")
    $mens.p(arg0)
    $mens.mtodos1("Diamante azul desaparece ao longe enquanto cai.")
    apagar(dentro1.objini("i_ini001"))
  fimse

func entrou0 # Fim dos eventos 'entrou'
  apagar(dentro1.objini("i_ini001"))

func cmd_pegarobj
  se "i_ini001" == arg2 && arg0.dentro1.objini("i_ini001")
    arg0.msg("Você tenta pegar, mas não consegue. O diamante está um pouco longe.")
    ret 1
  fimse


classe s_ini007
herda b_ini
const s_titulo = "Região oeste da ilha"
const s_desc = "Há várias pedras vermelhas no chão. Há também uma [placa] que pode ser lida\n\
teclando olhar placa. À leste fica o centro da ilha. Nas outras direções só\n\
se vê o céu azul e uma névoa branca no lugar do chão."
const dir_l = $s_ini002
const dir_n = $s_ini004
const dir_s = $s_ini004
const dir_o = $s_ini004
const eveste = 1 # Receber eventos do próprio objeto
const ver_placa = "A placa diz: Depósito de diamantes vermelhos\n\
Para pegar tecle pegar diamante_vermelho\n\
Para soltar tecle soltar diamante_vermelho\n\
Muitas vezes pode-se abreviar os comandos.\n\
Exemplo, \"pe dia\" ao invés de \"pegar diamante_vermelho\"."
const s_reset = "S 100 ini003 78 s"

func cmd_pegarobj
  se arg2 == "i_ini003"
    se arg0.dentro1.objini("i_ini003")
      arg0.msg("Só pode carregar um diamante vermelho nessa ilha.")
    senao
      criar("i_ini003", arg0)
      $mens.p(arg0)
      $mens.mtodos1("$P pega Diamante Vermelho.")
    fimse
    ret 1
  fimse

func cmd_soltarobj
  se arg2 == "i_ini003"
    apagar(arg2)
    $mens.p(arg0)
    $mens.mtodos1("$P solta Diamante Vermelho.")
    ret 1
  fimse

func s_reset1 # Realiza o reset
# Não alterar; função criada automaticamente a partir de s_reset
  !dentro1.objini("i_ini003") && criar("i_ini003", este, 78)


classe s_ini008
herda b_ini
const s_titulo = "Região sul da ilha"
const s_desc = "Afastando-se do centro da ilha, em direção ao sul, há uma velha cabana.\n\
Ao norte a ilha continua, mas tanto no leste quanto no oeste, vê-se apenas\n\
uma névoa. Nota-se uma pequena [placa] em cima da porta (para ler o que\n\
está escrito na placa tecle olhar placa)."
const eveste = 1 # Receber eventos do próprio objeto
const dir_n = $s_ini002
const dir_s = $s_ini009
const portaini_s = 2 # Estado inicial: 2 Porta fechada
const portanome_s = "a porta da cabana"
const dir_l = $s_ini004
const dir_o = $s_ini004
inttempo fechaporta # Para fazer o vento bater a porta
const ver_placa = "A placa diz:\nPara abrir a porta ao sul, tecle abrir s"

func cmd_abriudir
  se arg2 == "s"
    fechaporta = 50
  fimse

func fechaporta_exec
  se porta_s == 1
    mudaporta("s", 2)
    msg("O vento bateu a porta da cabana.")
    $s_ini009.msg("O vento bateu a porta da cabana.")
  fimse


classe s_ini009
herda b_ini
const s_titulo = "Dentro da cabana"
const s_desc = "Há uma janela. A vista é do céu, e no lugar do chão, há uma\n\
névoa branca como neve. Há uma pessoa esquisita aqui."
const dir_n = $s_ini008
const portaini_n = 2 # Inicialmente fechado
const portanome_n = "a porta da cabana"
const eveste = 1 # Receber eventos do próprio objeto
const s_reset = "P 100 ini002 27 27 1"

func cmd_abriudir
  se arg2 == "n"
    $s_ini008.fechaporta = 50
  fimse

func s_reset1 # Realiza o reset
# Não alterar; função criada automaticamente a partir de s_reset
  se p_ini002:objsolto.total < 1
    criar("p_ini002", este, 27)
  fimse


classe s_ini010
herda b_ini
const s_titulo = "Região norte da ilha"
const s_desc = "Há um pequeno arbusto aqui. Olhando mais atentamente, você nota uma\n\
escada para baixo escondida pelo arbusto. É uma escada comprida, de modo\n\
que ela desaparece na névoa. Nas outras direções você só vê uma névoa\n\
branca, exceto ao sul, aonde a ilha continua."
const dir_n = $s_ini004
const dir_s = $s_ini002
const dir_l = $s_ini004
const dir_o = $s_ini004
const dir_b = $s_ini011


classe s_ini011
herda b_ini
const s_titulo = "Escada"
const s_desc = "Logo acima, uma ilha. Em baixo não se vê aonde termina."
const dir_c = $s_ini010
const dir_b = $s_ini012


classe s_ini012
herda b_ini
const s_titulo = "Escada"
const s_desc = "Daqui só se vê a parte da escada onde você está segurando. Não se vê\n\
aonde ela começa ou termina. Essa sensação dá um mal estar."
const dir_c = $s_ini011
const dir_b = $s_ini013


classe s_ini013
herda b_ini
const s_titulo = "Escada"
const s_desc = "Logo abaixo, uma ilha. Em cima não se vê aonde termina."
const dir_c = $s_ini012
const dir_b = $s_ini014


classe s_ini014
herda b_ini
const s_titulo = "Pequena ilha"
const s_desc = "Um pouco menor que a ilha grande, mas a névoa continua a mesma.\n\
Há uma escada para cima, mas não se vê aonde ela vai dar."
const dir_c = $s_ini013
const dir_s = $s_ini015
const dir_l = $s_ini017
const eveste = 1 # Receber eventos do próprio objeto

func cmd_mover
  se arg2 == "c"
    se arg0.dentro2.objini("i_ini005")
      arg0.msg("Você está vestindo algo muito pesado, e por isso não consegue subir.")
      ret 1
    senao arg0.dentro1.objini("i_ini005")
      arg0.msg("Você está segurando algo muito pesado, e por isso não consegue subir.")
      ret 1
    fimse
  fimse


classe s_ini015
herda b_ini
const s_titulo = "Pequena ilha"
const s_desc = "Você está bem em baixo da ilha maior, mas ela não está visível daqui.\n\
A neblina está forte e a distância é razoável. Tudo que você vê é\n\
a continuação da ilha ao norte e ao sul."
const dir_n = $s_ini014
const dir_s = $s_ini016
const s_reset = "P 100 ini001 1 1 1"

func s_reset1 # Realiza o reset
# Não alterar; função criada automaticamente a partir de s_reset
  se p_ini001:objsolto.total < 1
    criar("p_ini001", este, 1)
  fimse


classe s_ini016
herda b_ini
const s_titulo = "Pequena ilha"
const s_desc = "A neblina aqui é forte, mas não o suficiente para ocultar uma placa.\n\
Pode-se ler o que está escrito na [placa]."
const dir_n = $s_ini015
const eveste = 1 # Receber eventos do próprio objeto
const ver_placa = "A placa diz: você pode saber o que está carregando e vestindo teclando\n\
inv e equip, respectivamente. Talvez você queira remover ou vestir\n\
alguma coisa (comandos são remover e vestir seguido do nome do item)."
const s_reset = "S 100 ini004 45 s"

func cmd_pegarobj
  se arg2 == "i_ini004"
    se arg0.dentro1.objini("i_ini004")
      arg0.msg("Só pode carregar um diamante verde nessa ilha.")
    senao
      criar("i_ini004", arg0)
      $mens.p(arg0)
      $mens.mtodos1("$P pega Diamante Verde.")
    fimse
    ret 1
  fimse

func cmd_soltarobj
  se arg2 == "i_ini004"
    apagar(arg2)
    $mens.p(arg0)
    $mens.mtodos1("$P solta Diamante Verde.")
    ret 1
  fimse

func s_reset1 # Realiza o reset
# Não alterar; função criada automaticamente a partir de s_reset
  !dentro1.objini("i_ini004") && criar("i_ini004", este, 45)


classe s_ini017
herda b_ini
const s_titulo = "Pequena ilha"
const s_desc = "Ao norte há uma ponte de cordas, que não parece muito segura. A madeira\n\
que compõe o chão da ponte está bastante gasta, e as cordas não estão\n\
em muito melhor estado."
const dir_o = $s_ini014
const dir_n = $s_ini018


classe s_ini018
herda b_ini
const s_terreno = 5
const s_titulo = "Ponte de cordas"
const s_desc = "Você está em uma extremidade da ponte. Conforme o vento bate,\n\
a ponte balança um pouco. Felizmente há terra firme ao sul."
const dir_s = $s_ini017
const dir_n = $s_ini019


classe s_ini019
herda b_ini
const s_terreno = 5
const s_titulo = "Ponte de cordas"
const s_desc = "Há muita neblina, de modo que não se vê aonde a ponte começa ou termina.\n\
Conforme o vento bate, a ponte balança e você se segura para não cair.\n\
Algumas táboas estão quebradas aqui."
const dir_s = $s_ini018
const dir_n = $s_ini020


classe s_ini020
herda b_ini
const s_terreno = 5
const s_titulo = "Ponte de cordas"
const s_desc = "Você está em uma extremidade da ponte. Conforme o vento bate,\n\
a ponte balança um pouco. Há terra firme ao norte."
const dir_s = $s_ini019
const dir_n = $s_ini021


classe s_ini021
herda b_ini
const s_titulo = "Pequena ilha"
const s_desc = "Aqui se vê uma ponte de cordas bem gasta ao sul."
const dir_s = $s_ini020
const s_reset = "S 100 ini006 1 s\nP 100 ini003 31 31 1"

func s_reset1 # Realiza o reset
# Não alterar; função criada automaticamente a partir de s_reset
  !dentro1.objini("i_ini006") && criar("i_ini006", este, 1)
  se p_ini003:objsolto.total < 1
    criar("p_ini003", este, 31)
  fimse


classe s_ini022
herda b_ini
const s_titulo = "Movimentação"
const s_desc = "Para mover-se por nosso mundo, você poderá usar as seguintes direções:\n\
N (norte), S (sul), l (leste), o (oeste), c (cima), b (baixo),\n\
ne (nordeste), no (noroeste), se (sudeste), so (sudoeste), entrar e sair."
const dir_s = $s_ini001
const dir_l = $s_ini023


classe s_ini023
herda b_ini
const s_titulo = "Abrindo e fechando portas"
const s_desc = "Você poderá encontrar portas fechadas pelo caminho. Nesse caso, você\n\
precisará antes abrir a porta na direção para prosseguir. Para abrir uma\n\
porta em uma direção você pode digitar abrir e a direção, por exemplo: abrir\nl."
const dir_l = $s_ini024
const dir_o = $s_ini022
const s_reset = "D 100 l 2"

func s_reset1 # Realiza o reset
# Não alterar; função criada automaticamente a partir de s_reset
  mudaporta("l", 2)


classe s_ini024
herda b_ini
const s_titulo = "Comunicação entre jogadores"
const s_desc = "Os principais comandos de comunicação do jogo são os seguintes:\n\
chat\n\
Envia mensagem para todo o mud.\n\
falar\n\
Envia mensagem para os jogadores que estiverem na mesma sala.\n\
sussurrar\n\
Envia mensagem para um jogador na mesma sala.\n\
telepatia\n\
Envia mensagem para um jogador, independente da sala.\n\
gritar\n\
Envia mensagem para todos os jogadores na mesma área.\n\
Para mais comandos, consulte o tópico comunica, no comando ajuda."
const dir_l = $s_ini025
const dir_o = $s_ini023


classe s_ini025
herda b_ini
const s_titulo = ",Manuseando objetos"
const s_desc = "Os comandos básicos para lidar com um objeto são os seguintes:\n\
pegar\n\
Pega um objeto, do chão ou de dentro de outro objeto\n\
soltar\n\
Solta um objeto, no chão ou dentro de outro objeto.\n\
Guardar\n\
Guarda um objeto dentro de outro objeto.\n\
Dar\n\
Dá um objeto a outro jogador ou personagem\n\
vestir/empunhar\n\
Veste um objeto ou o empunha, caso seja uma arma.\n\
comer/beber\n\
come ou bebe algo"
const dir_o = $s_ini024
