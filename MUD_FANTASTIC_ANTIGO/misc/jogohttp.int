classe jogohttp
# HTTP rodando independente
serv servidor # Para receber conexões
indiceobj conexao1 # "cn " + porta
txt100 conexao2 # Texto comando COM: mesmo que arg0 na função ini

func ini
# arg0 contém 3 textos separados por espaço: tipo, endereço, porta
# Tipo deve ser "http"
# Se o endereço for "*", abre a porta em todos os endereços
  conexao1 = "cn " + txtsub(arg0, 2)
  conexao2 = arg0
  refvar ender = txtsub(arg0, 1, 1)
  refvar t1 = servidor.abrir(ender == "*" ? "" : ender, txtsub(arg0, 2))
  se t1 != "1"
    msg("Não foi possível abrir a porta")
    apagar(este), conexao2 = ""
    ret
  fimse
  msg("Esperando conexões")

func fim
  conexao2 && msg("Conexão encerrada")

func msg
  se !config:modolocal
    telatxt tela
    tela.msg("Serv " + conexao2 + ": " + arg0 + "\n")
  fimse
  indiceitem item
  refvar m = "\b\d1(com) " + conexao2 + ": " + arg0 + "\b"
  epara item.ini("un "), txt1(item.txt) == "un", item.depois
    item.obj.jogconfig.21 && item.obj.msg(m)
  efim

func servidor_socket # Conectado: cria objeto
  criar("jogohttp_ini", arg0, este)


classe jogohttp_ini
socket conec # Usuário conectado
inttempo tempo # Para encerrar conexão após um tempo
uint8 passo # 0=definindo se Papovox, 1=entrando Papovox, 2=dentro
uint8 penv # Para enviar página HTML
txt100 host # Conteúdo da linha Host
txt100 origin # Conteúdo da linha Origin
txt100 key

func ini
# arg0 = socket
# arg1 = objeto jogohttp
  conec = arg0
  tempo = 80

func tempo_exec
  apagar(este)

func conec_msg
# telatxt t
# t.msg(">" + passo + "/" + arg0 + "\n")
  casovar passo
  casose "0" # Primeira linha do cabeçalho
    se txt1(arg0) != "GET"
      apagar(este)
    senao txtsub(arg0, 1, 1) == "/ws"
      passo = 2
    senao txtsub(arg0, 1, 1) != "/"
      conec.msg("HTTP/1.0 404 NOT FOUND\n\n")
    senao
      passo = 1
    fimse
    ret
  casose "1" # Página HTML: esperando fim do cabeçalho
    se txt1(arg0) == "host:"
      host = txt2(arg0)
    senao txt1(arg0) == "origin:"
      origin = txt2(arg0)
    senao arg0 != ""
    senao host == ""
      apagar(este)
    senao
      tempo = 100
      passo = 10
      conec_env
    fimse
    ret
  casose "2" # WebSocket
  casose "3"
    se txt1(arg0) == "host:"
      host = txt2(arg0)
    senao txt1(arg0) == "origin:"
      origin = txt2(arg0)
    senao txt1(arg0) == "Sec-WebSocket-Key:"
      key = txt2(arg0)
    senao txt1(arg0) == "Upgrade:" && txt2(arg0) == "WebSocket"
      passo = 3
    senao arg0 != ""
    senao host == "" || key == "" || passo != 3
      apagar(este)
    senao
      txt(origin, 0, 7) == "http://" && (origin = txt(origin, 7))
      txt(host, 0, 7) == "http://" && (host = txt(host, 7))
      ret origin != host, apagar(este) # Somente conexões do mesmo servidor
      refvar t1 = txtconv(txtsha1(key + "258EAFA5-E914-47DA-95CA-C5AB0DC85B11"), "hb")
# telatxt t
# t.msg("KEY  [" + key + "]\nRESP [" + t1 + "]\n")
      conec.msg("HTTP/1.1 101 Switching Protocols\n")
      conec.msg("Upgrade: websocket\n")
      conec.msg("Connection: Upgrade\n")
      conec.msg("Sec-WebSocket-Accept: " + t1 + "\n\n")
      conec.proto = 6
# conec.msg("Sec-WebSocket-Protocol: chat\n\n")
# Sec-WebSocket-Origin: http://" + host + "\n")
# conec.msg("Sec-WebSocket-Location: ws://" + host + "\n")
      apagar(este)
      criar("jogohttp_usr", conec)
    fimse
    ret
  casose "10"
    ret
  casose
    apagar(este)
    ret
  casofim

func conec_env
  se passo == 4
  senao !msg_[passo]
    apagar(este)
  senao conec.msg(txtconv(msg_[passo], "tu"))
    passo++
  fimse

func fim
# telatxt t
# t.msg(">FECHOU\n")

const msg_10 = "HTTP/1.0 200 OK\n\
Server: MUD\n\
Pragma: no-cache\n\
Cache-Control: max-age=0\n\
Expires: Thu, 01 Jan 1970 00:00:00 GMT\n\
Content-Type: text/html; charset=utf-8\n\n"
#
const msg_11 = "<!DOCTYPE html>\n\
<html style=\"padding: 5px;\" lang=\"en\">\n\
<head><title></title>\n\
<meta http-equiv=\"content-type\" content=\"text/html; charset=UTF-8\">\n\
  <style type=\"text/css\">\n\
  <!--\n\
    body {\n\
      background-color: #1D1D1D;\n\
      color: #C0C0C0;\n\
      height: 97%;\n\
      width: 100%;\n\
      min-width: 700px;\n\
      margin: 0;\n\
      padding: 0;\n\
      font-size: 85%;\n\
    }\n\
    .term {\n\
      font-family: Monaco,Consolas,Lucida Console,DejaVu Sans Mono,Courier New,Droid Sans \
Mono,monospace;\n\
      font-size: 100%;\n\
      background-color: #1D1D1D;\n\
      color: #C0C0C0;\n\
    }\n\
    #opcoes { position:fixed; z-index: 1; right: 10px; top: 5px; font-size: 12px; }\n\
    #botaoguia { border:1px solid #888; color: white; background-color: #222; }\n\
    #botaoguia:hover { background-color: white; color: black; }\n\
    #botaoguia:focus { text-decoration: underline; }\n\
    .guia { position:fixed; z-index: 1; width: 320px; right: 10px; top: 30px; display: none; \
border:1px solid #888; padding: 1px 3px;\n"
#
const msg_12 = "      background-color:#000; font-family: Verdana,Arial,Helvetica,sans-serif; \
font-size: 12px; }\n\
    .guia button { color: white; background-color:#111; font-size: 12px; margin: 1px 1px; }\n\
    .guia button:hover { background-color: white; color: black; }\n\
    .guia button:focus { text-decoration: underline; }\n\
    .move1 { height:auto; width:30px; border:1px solid #888; }\n\
    .move2 { height:0; width:30px; border:0; padding:0; }\n\
    .envia { height:auto; border:1px solid #888; }\n\
    .opcao { border:0; }\n\
    .guia h1 { background-color:#353; font-size: 120%; margin-top: 0; }\n\
    .guia h2 { background-color:#242; font-size: 100%; margin: 3px 3px; }\n\
  -->\n\
  </style>\n\
</head>\n\
<body>\n\
  <noscript style=\"color: #ff0000;\">Please enable Javascript.</noscript>\n\
  <div style=\"height: auto; width: auto; margin: 0 0 40px 0;\">\n\
    <pre class=\"term\" id=\"terminal\" style=\"height: auto; width: 100%; margin: 0 auto;\"\n\
      aria-relevant=\"text additions\" aria-atomic=\"false\" aria-live=\"polite\">\n"
#
const msg_13 = "    <span><br></span></pre>\n\
  </div>\n\
  <div style=\"position: fixed; top: auto; bottom: 0; left: 0; right: 0; height: 40px; background\
-color:#1D1D1D;\">\n\
    <input type=\"text\" size=\"77\" name=\"i\" id=\"cmd\" class=\"term\" style=\"margin: 11px \
2px; border:1px solid #888;\">\n\
    <div style=\"margin: 0em\">\n\
    <img id=\"vida\" src=\"data:image/gif;base64,R0lGODlhCgAFAIACAAAAAM4yMiH+EUNyZWF0ZWQgd2l0aCBH\
SU1QACwAAAAACgAFAAACCYSPocvtr5AEBQA7\"\n\
      style=\"left:10px; top:2px; position: absolute;\" height=5 width=2\n\
      alt=\"Pontos de vida\">\n\
    <img id=\"mana\" src=\"data:image/gif;base64,R0lGODlhCgAFAIACAAAAADLONiwAAAAACgAFAAACCYSPocvt\
r5AEBQA7\"\n\
      style=\"left:220px; top:2px; position: absolute;\" height=5 width=2\n\
      alt=\"Pontos de mana\">\n\
    <img id=\"move\" src=\"data:image/gif;base64,R0lGODlhCgAFAIACAAAAADIyziwAAAAACgAFAAACCYSPocvt\
r5AEBQA7\"\n\
      style=\"left:430px; top:2px; position: absolute;\" height=5 width=2\n\
      alt=\"Pontos de movimento\">\n\
    </div>\n"
#
const msg_14 = "    <img id=\"exp\" src=\"data:image/gif;base64,R0lGODlhCgADAIABAJ6env///ywAAAAAC\
gADAAACBYSPqWsFADs=\"\n\
      style=\"left:10px; top:7px; position: absolute;\" height=2 width=2\n\
      alt=\"Pontos para subir de nível\">\n\
  </div>\n\
  <div id=\"opcoes\">\n\
    <input type=\"checkbox\" id=\"chkbipe\" name=\"chkbipe\" value=\"1\">\n\
    <label for=\"chkbipe\">Som ao chegar mensagem</label>\n\
    <input type=\"checkbox\" id=\"chkrolagem\" name=\"chkrolagem\" value=\"checked\" checked>\n\
    <label for=\"chkrolagem\">Rolagem automática</label>\n\
    <button id=\"botaoguia\">Comandos</button>\n\
  </div>\n\
  <div id=\"guia\" class=\"guia\">\n\
    <h1>Principais comandos</h1>\n\
    <div id=\"guia1\"></div>\n\
    <div id=\"guia2\"></div>\n\
  </div>\n\
<audio id=\"sombipe\" style=\"display: none;\">\n\
<source type=\"audio/mpeg\" src=\"data:audio/mp3;base64,//NAxAAQ60rS\n\
UUEQASK7c2VwGYx3MC4xuAC+MhP536EJ/53/kJ58hCN8hCEIQmQhCEIRpCE/////\n\
5z//+c53+c7///53/Oc7znOc53kDizh6I1h0d2dneINXkpdAhYQA8mwbVoycnbj/\n\
80LEFxnzKvcdjCgBtc1Ra7R1cVFQQTESgYSEgKZzOQPAYXMJCZu4fVgxyCQ063lD\n"
#
const msg_15 = "p3KHTs1Sms7vVSEntETOK//6+s/6UnZ/Rf120OJmJ0zU+yFKn//s13/r8SRqeAmQeAiQmQeAwG7/80DEC\
xcCczMfizgCOBAKBgAESUk/x0kIo7yc3j2JDVPqAKZrxGJDpLX41FQ8PiR/5YHxUdIEzk9f8SR5WPciNh7//+qmHnGoh7Gj3/\
//+Kx8WExsOsBkk9X//pWWTEFNRTMuOTkuNf/zQsQKAAADSAHAAACqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqq\
qqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqg==\">\n\
</audio>\n\
<script><!--\n\n\
var historico = [ ]; // Histórico do que foi digitado\n\
var histnum = 0; // Em qual linha do histórico está\n\
var conectado = false; // Se está conectado\n\
var conec = null; // Conexão WebSocket\n\n\
var corfrente = 7; // 0-15\n\
var corfundo = 0; // 0-15\n\
var coratrib = 0; // +1=sublinhado +2=piscante +4=troca frente/fundo\n\n\
// Add and remove events, based on\n\
// http://www.javascripter.net/faq/addeventlistenerattachevent.htm\n\
function addEventHandler(obj,eventType,handler) {\n\
  var elem = document.getElementById(obj);\n\
  if (elem.addEventListener) elem.addEventListener (eventType,handler,false);\n"
#
const msg_16 = "  else if (elem.attachEvent) elem.attachEvent ('on'+eventType,handler);\n\
}\n\n\
function removeEventHandler(obj,eventType,handler) {\n\
  var elem = document.getElementById(obj);\n\
  if (elem.removeEventListener) elem.removeEventListener (eventType,handler,false);\n\
  if (elem.detachEvent)         elem.detachEvent ('on'+eventType,handler);\n\
}\n\n\
// Adiciona texto no terminal\n\
function env_tela(msg) {\n\
  var term = document.getElementById('terminal');\n\
  while (term.childElementCount >= 300)\n\
    term.removeChild(term.firstChild);\n\
  var node = document.createElement('span');\n\
  node.innerHTML = msg + \"\\n\";\n\
  term.appendChild(node);\n\
  if (document.getElementById('chkrolagem').checked)\n\
    window.scrollTo(0,document.body.scrollHeight);\n\
  return node;\n\
}\n\n\
// Retorna o código da cor a partir do número da cor, de 0 a 15\n\
function estilo_cor(cor) {\n\
  switch (cor)\n\
  {\n\
    case 0:  return \"#1D1D1D;\"; break; // BLACK\n\
    case 1:  return \"#B00000;\"; break; // RED\n\
    case 2:  return \"#00B000;\"; break; // GREEN\n"
#
const msg_17 = "    case 3:  return \"#B0B000;\"; break; // YELLOW\n\
    case 4:  return \"#0000B0;\"; break; // BLUE\n\
    case 5:  return \"#B000B0;\"; break; // PURPLE\n\
    case 6:  return \"#00B0B0;\"; break; // CYAN\n\
    case 7:  return \"#C0C0C0;\"; break; // WHITE\n\
    case 8:  return \"#808080;\"; break; // BLACK\n\
    case 9:  return \"#FF0000;\"; break; // RED\n\
    case 10: return \"#00FF00;\"; break; // GREEN\n\
    case 11: return \"#FFD700;\"; break; // YELLOW\n\
    case 12: return \"#0000FF;\"; break; // BLUE\n\
    case 13: return \"#FF00FF;\"; break; // PURPLE\n\
    case 14: return \"#00FFFF;\"; break; // CYAN\n\
    case 15: return \"#FFFFFF;\"; break; // WHITE\n\
  }\n\
  return \"#000000\";\n\
}\n\n\
// Retorna o estilo (um texto) a partir dos atributos de cor\n\
function estilo_atrib() {\n\
  var estilo = \"\";\n\
  if ((coratrib & 4) != 0) {\n\
    estilo += \"color:\" + estilo_cor(corfundo);\n\
    estilo += \"background-color:\" + estilo_cor(corfrente);\n\
  }\n\
  else {\n\
    estilo += \"color:\" + estilo_cor(corfrente);\n"
#
const msg_18 = "    estilo += \"background-color:\" + estilo_cor(corfundo);\n\
  }\n\
  if ((coratrib & 3) == 1)\n\
    estilo += \"text-decoration:underline;\";\n\
  else if ((coratrib & 3) == 2)\n\
    estilo += \"text-decoration:blink;\";\n\
  else if ((coratrib & 3) == 3)\n\
    estilo += \"text-decoration:underline blink;\";\n\
  return estilo;\n\
}\n\n\
// Chegou mensagem do servidor\n\
function recv_message(e) {\n\
  //console.log(\">> recv_message: \" + e.data.length + \" = \" + e.data);\n\
  if (e.data.charAt(0) == '+')\n\
    msg_tela(e.data.substring(1));\n\
  else if (e.data.charAt(0) == 's') {\n\
    var res = e.data.substring(1).split(' ');\n\
    if (res.length >= 4) {\n\
      document.getElementById('vida').width = 2 * res[0];\n\
      document.getElementById('vida').alt = 'Vida ' + res[0] + '%';\n\
      document.getElementById('mana').width = 2 * res[1];\n\
      document.getElementById('mana').alt = 'Mana ' + res[1] + '%';\n\
      document.getElementById('move').width = 2 * res[2];\n\
      document.getElementById('move').alt = 'Movimentos ' + res[2] + '%';\n"
#
const msg_19 = "      document.getElementById('exp').width = 620 * res[3] / 100;\n\
      document.getElementById('exp').alt = 'Experiência ' + res[3] + '%';\n\
    }\n\
  }\n\
}\n\n\
function msg_tela(str) {\n\
  var ret = \"\"; // Texto que será enviado para a tela\n\
  var tam = 0; // Quantos caracteres na tela\n\
  var i = 0;\n\
  var est_ini = estilo_atrib();\n\
  var completa = (((coratrib) & 4) ? (corfrente != 0) : (corfundo != 0));\n\
  var estilo = \"\";\n\
  var atualizar = false;\n\
  var barra = false;\n\n\
  while (i < str.length)\n\
  {\n\
    if (str.charCodeAt(i) < 32 && str.charCodeAt(i) >= 0) {\n\
      i++;\n\
      continue;\n\
    }\n\
    var ch = str.charAt(i++);\n\
    if (ch != '\\\\' || barra) {\n\
      barra = false;\n\
      if (atualizar) {\n\
        atualizar = false;\n\
        var novo = estilo_atrib();\n\
        if (novo != estilo) {\n\
          if (estilo != \"\")\n\
            ret += \"</span>\";\n\
          estilo = novo;\n\
          if (estilo != \"\")\n\
            ret += \"<span style=\\\"\" + estilo + \"\\\">\";\n\
        }\n\
      }\n\
      if (ch == \"<\")\n"
#
const msg_20 = "        ret += \"&lt;\";\n\
      else if (ch == \">\")\n\
        ret += \"&gt;\";\n\
      else if (ch == \"&\")\n\
        ret += \"&amp;\";\n\
      else\n\
        ret += ch;\n\
      if (++tam >= 80) {\n\
        env_tela(ret).style = est_ini;\n\
        est_ini = estilo_atrib();\n\
        tam = 0;\n\
        ret = \"\";\n\
        completa = ((coratrib & 4 ? corfrente : corfundo) == 7);\n\
      }\n\
      continue;\n\
    }\n\
    if (i >= str.length)\n\
      break;\n\
    ch = str.charAt(i++);\n\
    if (ch == '\\\\' || ch == '\"') {\n\
      barra = true; i--;\n\
    }\n\
    else if (ch == 'b') {\n\
      corfrente = 7;\n\
      corfundo = 0;\n\
      coratrib = 0;\n\
      atualizar = true;\n\
    }\n\
    else if (ch == 'd') {\n\
      if (i >= str.length)\n\
        break;\n\
      switch (str.charAt(i++))\n\
      {\n\
      case '0': corfundo = 0; break;\n\
      case '1': corfundo = 1; break;\n\
      case '2': corfundo = 2; break;\n\
      case '3': corfundo = 3; break;\n\
      case '4': corfundo = 4; break;\n\
      case '5': corfundo = 5; break;\n\
      case '6': corfundo = 6; break;\n"
#
const msg_21 = "      case '7': corfundo = 7; break;\n\
      }\n\
      atualizar = true;\n\
    }\n\
    else if (ch == 'c') {\n\
      if (i >= str.length)\n\
        break;\n\
      switch (str.charAt(i++).toLocaleLowerCase())\n\
      {\n\
      case '0': corfrente = 0; break;\n\
      case '1': corfrente = 1; break;\n\
      case '2': corfrente = 2; break;\n\
      case '3': corfrente = 3; break;\n\
      case '4': corfrente = 4; break;\n\
      case '5': corfrente = 5; break;\n\
      case '6': corfrente = 6; break;\n\
      case '7': corfrente = 7; break;\n\
      case '8': corfrente = 8; break;\n\
      case '9': corfrente = 9; break;\n\
      case 'a': corfrente = 10; break;\n\
      case 'b': corfrente = 11; break;\n\
      case 'c': corfrente = 12; break;\n\
      case 'd': corfrente = 13; break;\n\
      case 'e': corfrente = 14; break;\n\
      case 'f': corfrente = 15; break;\n\
      case 'g': coratrib |=  1; break; // + sublinhado\n\
      case 'h': coratrib &= ~1; break; // - sublinhado\n\
      case 'i': coratrib |=  4; break; // + inverter frente/fundo\n"
#
const msg_22 = "      case 'j': coratrib &= ~4; break; // - inverter frente/fundo\n\
      case 'k': coratrib |=  2; break; // + piscante\n\
      case 'l': coratrib &= ~2; break; // - piscante\n\
      case 'm': // Echo off\n\
        document.getElementById('cmd').type = 'password';\n\
        break;\n\
      case 'n': // Echo on\n\
        document.getElementById('cmd').type = 'text';\n\
        break;\n\
      }\n\
      atualizar = true;\n\
    }\n\
  }\n\
  if (estilo != \"\")\n\
    ret += \"</span>\";\n\
  if (tam != 0) {\n\
    while (completa && tam < 80) {\n\
      ret += ' ';\n\
      tam++;\n\
    }\n\
    env_tela(ret).style = est_ini;\n\
  }\n\
  if (document.getElementById('chkbipe').checked) {\n\
    var bell = document.getElementById('sombipe');\n\
    if (!bell.paused) {\n\
      bell.pause();\n\
      bell.currentTime = 0;\n\
    }\n\
    bell.play();\n\
  }\n\
}\n\n\
// Usuário pressionou uma tecla\n\
function tecla(e) {\n\
  var keysym = (e ? e : window.event).keyCode;\n\
  var cmd = document.getElementById('cmd');\n\
  //console.log(\"key \" + keysym);\n\n\
  switch (keysym)\n  {\n"
#
const msg_23 = "  case 38: // Seta para cima\n\
    if (histnum != 0)\n\
      cmd.value = historico[--histnum];\n\
    break;\n\
  case 40: // Seta para baixo\n\
    if (histnum + 1 == historico.length) {\n\
      cmd.value = \"\";\n\
      histnum++;\n\
    }\n\
    else if (histnum < historico.length)\n\
      cmd.value = historico[++histnum];\n\
    break;\n\
  case 13: // Enter\n\
    if (!conectado)\n\
      break;\n\
    //console.log(\"Enviar \" + cmd.value);\n\
    conec.send(\"+\" + cmd.value);\n\
    if (cmd.type == 'text') {\n\
      if (historico.length == 0)\n\
        historico.push(cmd.value)\n\
      else if (historico[historico.length - 1] != cmd.value)\n\
        historico.push(cmd.value)\n\
      if (historico.length > 100)\n\
        historico.shift();\n\
      histnum = historico.length;\n\
    }\n\
    cmd.value = \"\";\n\
    break;\n\
  }\n\n\
  return 0;\n\
}\n\n\
window.onload = function() {\n\
  document.getElementById(\"cmd\").focus();\n\
  addEventHandler(\"cmd\", \"keydown\", tecla);\n\
  addEventHandler(\"botaoguia\", \"click\", janelaguia);\n\
  if (window.WebSocket) {\n"
#
const msg_24 = "    msg_tela(\"Setas \\\\cbCIMA\\\\b e \\\\cbBAIXO\\\\b acessam o histórico do \
que você digitou\");\n\
    env_tela(\"\");\n\
    conectar();\n\
  }\n\
  else {\n\
    env_tela(\"\\n\\nSeu navegador não suporta WebSocket;\\n\");\n\
    env_tela(\"Use um navegador mais recente\");\n\
    document.getElementById('cmd').style.display = 'none';\n\
  }\n\
  criarguia();\n\
}\n\n\
function conectar() {\n\
  msg_tela(\"Conectando em \" + endserv);\n\
  conec = new WebSocket('ws://' + endserv + '/ws');\n\
  conec.onmessage = recv_message;\n\
  conec.onopen = function() {\n\
    console.log(\">> WebSock.onopen\");\n\
    conectado = true;\n\
    msg_tela(\"Conectado!\");\n\
    //conec.send('Ping');\n\
  };\n\
  conec.onerror = function (error) {\n\
    console.log('WebSocket Error ' + error);\n\
    if (!conectado) {\n\
      msg_tela(\"\\\\cF\\\\d1Erro ao conectar\");\n\
      return;\n\
    }\n\
    conec.close();\n\
    conectado = false;\n\
    msg_tela(\"\\\\cF\\\\d1Desconectado\");\n\
  };\n\
  conec.onclose = function(error) {\n\
    console.log('WebSocket Error ' + error);\n\
    if (!conectado)\n\
      return;\n"
#
const msg_25 = "    conectado = false;\n\
    msg_tela(\"\\\\cF\\\\d1Desconectado\");\n\
  };\n\
}\n\n\
function janelaguia() {\n\
  var guia = document.getElementById('guia');\n\
  if (guia.style.display != \"block\")\n\
    guia.style.display = \"block\";\n\
  else\n\
    guia.style.display = \"none\";\n\
}\n\n\
var ajudafim = null;\n\n\
function criarguia() {\n\
  var guia1 = document.getElementById('guia1');\n\
  var guia2 = document.getElementById('guia2');\n\
  for (var i = 0; i < TextoGuia.length; i++) {\n\
    if (TextoGuia[i][0] == \"titulo\") {\n\
      var node = document.createElement(\"h2\");\n\
      node.innerHTML = TextoGuia[i][1];\n\
      guia1.appendChild(node);\n\
      //guia1.appendChild(document.createTextNode(TextoGuia[i][1]));\n\
      continue;\n\
    }\n\
    if (TextoGuia[i][0] == \"br\") {\n\
      guia1.appendChild(document.createElement(\"br\"));\n\
      continue;\n\
    }\n\
    var node1 = document.createElement(\"button\");\n\
    node1.innerHTML = TextoGuia[i][1];\n\
    node1.className = TextoGuia[i][0];\n\
    node1.id = \"cmd-\" + i;\n\
    node1.onmouseover = function() {\n"
#
const msg_26 = "      if (ajudafim != null)\n\
        ajudafim.style.display = \"none\";\n\
      var e = document.getElementById(\"ajuda-\" + this.id);\n\
      e.style.display = \"block\";\n\
      ajudafim = e;\n\
    };\n\
    //node1.onmouseout = function() {\n\
    //  var e = document.getElementById(\"ajuda-\" + this.id);\n\
    //  e.style.display = \"none\";\n\
    //};\n\
    node1.onclick = function() {\n\
      if (this.className != \"opcao\") {\n\
        conec.send(\"+\" + this.firstChild.nodeValue);\n\
        return;\n\
      }\n\
      document.getElementById('cmd').value = this.firstChild.nodeValue + \" \";\n\
      document.getElementById(\"cmd\").focus();\n\
    };\n\
    guia1.appendChild(node1);\n\n\
    var node2 = document.createElement(\"div\");\n\
    node2.innerHTML = \"<h2>\" + TextoGuia[i][1] + \"</h2>\" + TextoGuia[i][2];\n\
    node2.id = \"ajuda-cmd-\" + i;\n\
    node2.style.display = \"none\";\n\
    guia2.appendChild(node2);\n\
  }\n\
}\n\n\
var TextoGuia = [\n\
  [\"titulo\", \"Movimentação\"],\n\
  [\"move1\", \"NO\", \"Anda para o noroeste.\"],\n"
#
const msg_27 = "  [\"move1\", \"N\",  \"Anda para o norte.\"],\n\
  [\"move1\", \"NE\", \"Anda para o nordeste.\"],\n\
  [\"br\"],\n\
  [\"move1\", \"O\", \"Anda para o oeste.\"],\n\
  [\"move2\", \" \", \"\"],\n\
  [\"move1\", \"L\", \"Anda para o leste.\"],\n\
  [\"envia\", \"Cima\", \"Vai para cima.\"],\n\
  [\"envia\", \"Entrar\", \"Entra (exceto em objetos).\"],\n\
  [\"envia\", \"Ver\", \"Mostra resumidamente aonde você está.\"],\n\
  [\"br\"],\n\
  [\"move1\", \"SO\", \"Anda para o sudoeste.\"],\n\
  [\"move1\", \"S\",  \"Anda para o sul.\"],\n\
  [\"move1\", \"SE\", \"Anda para o sudeste.\"],\n\
  [\"envia\", \"Baixo\", \"Vai para baixo.\"],\n\
  [\"envia\", \"Sair\", \"Sai.\"],\n\
  [\"envia\", \"Olhar\", \"Mostra aonde você está.\"],\n\
  [\"br\"],\n\
  [\"envia\", \"dormir\", \"Dorme, se estiver acordado. Assim, recupera mais rapidamente.\"],\n\
  [\"envia\", \"acordar\", \"Acorda, se estiver dormindo.\"],\n\
  [\"envia\", \"descansar\", \"Descansa. Recupera um pouco mais rápido.\"],\n\
  [\"envia\", \"sentar\", \"Senta. No entanto, para sentar-se em algum objeto, você deve teclar \
SENTAR seguido do nome do objeto.\"],\n"
#
const msg_28 = "  [\"envia\", \"levantar\", \"Levanta.\"],\n\
  [\"opcao\", \"abrir\", \"Abre uma porta ou um objeto. Exemplo, ABRIR N abre a porta ao norte, \
ABRIR BOLSA abre uma mala.\"],\n\
  [\"opcao\", \"fechar\", \"Fecha uma porta ou um objeto. Exemplo, FECHAR N fecha a porta ao \
norte, FECHAR BOLSA fecha uma mala.\"],\n\
  [\"opcao\", \"trancar\", \"Tranca uma porta ou um objeto. Exemplo, TRANCAR N tranca a porta ao \
norte, TRANCAR BOLSA tranca uma mala.\"],\n\
  [\"opcao\", \"destrancar\", \"Destranca uma porta ou um objeto. Exemplo, DESTRANCAR N destranca\
 a porta ao norte, DESTRANCAR BOLSA destranca uma mala.\"],\n\n\
  [\"titulo\", \"Informação\"],\n\
  [\"envia\", \"ajuda\", \"Mostra os tópicos de ajuda do jogo. Para saber sobre um tópico ou \
comando, teclar AJUDA seguido do nome. Exemplo, AJUDA VER.\"],\n\
  [\"envia\", \"quem\", \"Mostra os jogadores online que estão visíveis a você.\"],\n\
  [\"envia\", \"onde\", \"Mostra a área aonde você está e os jogadores que estão na mesma \
área.\"],\n\
  [\"envia\", \"vida\", \"Mostra os seus pontos de vida.\"],\n"
#
const msg_29 = "  [\"envia\", \"moedas\", \"Mostra quantas moedas você possui.\"],\n\
  [\"envia\", \"estat\", \"Mostra as estatísticas do eu personagem.\"],\n\
  [\"envia\", \"efeito\", \"Mostra os efeitos que estão afetando o seu personagem.\"],\n\
  [\"envia\", \"equip\", \"Mostra os itens que seu personagem está vestindo ou usando. Pode-se \
ainda ver todas as posições teclando EQUIP TUDO.\"],\n\
  [\"envia\", \"hab\", \"Mostra as habilidades do seu personagem. É possível ainda saber \
detalhes sobre uma habilidade teclando HAB e o nome da habilidade.\"],\n\
  [\"envia\", \"magia\", \"Mostra as magias do seu personagem. É possível ainda saber detalhes \
sobre uma magia teclando MAGIA e o nome da magia.\"],\n\
  [\"opcao\", \"ver\", \"Mostra resumidamente informações sobre um item ou personagem. Exemplo, \
VER BOLSA. Teclando apenas VER mostra aonde você está.\"],\n\
  [\"opcao\", \"olhar\", \"Mostra informações sobre um item ou personagem. Exemplo, OLHAR BOLSA. \
Teclando apenas OLHAR mostra aonde você está.\"],\n\n\
  [\"titulo\", \"Objetos\"],\n"
#
const msg_30 = "  [\"opcao\", \"pegar\", \"Pega um objeto. Exemplo, PEGAR CHAVE.\"],\n\
  [\"opcao\", \"soltar\", \"Solta um objeto. Exemplo, SOLTAR CHAVE.\"],\n\
  [\"opcao\", \"guardar\", \"Coloca um objeto dentro de outro. Exemplo, GUARDAR CHAVE BOLSA.\"],\n\
  [\"opcao\", \"dar\", \"Dá alguma coisa para alguém. Exemplo, DAR CHAVE GUARDA.\"],\n\
  [\"opcao\", \"empunhar\", \"Empunha uma arma. Exemplo, EMPUNHAR ESPADA.\"],\n\
  [\"opcao\", \"vestir\", \"Veste um objeto. Exemplo, VESTIR CAMISA.\"],\n\
  [\"opcao\", \"remover\", \"Remove um objeto que esteja vestindo ou empunhando. Exemplo, \
REMOVER CAMISA.\"],\n\
  [\"opcao\", \"acender\", \"Acende um objeto. Exemplo, ACENDER VELA.\"],\n\
  [\"opcao\", \"apagar\", \"Apaga um objeto. Exemplo, APAGAR VELA.\"],\n\
  [\"opcao\", \"beber\", \"Bebe alguma coisa. Exemplo, BEBER CANTIL.\"],\n\
  [\"opcao\", \"comer\", \"Come alguma coisa. Exemplo, COMER PÃO.\"],\n\
  [\"opcao\", \"recitar\", \"Recita um pergaminho.\"],\n\n\
  [\"titulo\", \"Comunicação\"],\n"
#
const msg_31 = "  [\"opcao\", \"falar\", \"FALAR &lt;mensagem&gt;<br>Fala para quem estiver na \
mesma sala. Exemplo, FALAR bom dia!\"],\n\
  [\"opcao\", \"gritar\", \"GRUTAR &lt;mensagem&gt;<br>Grita para quem estiver na mesma área. \
Exemplo, GRITAR bom dia!\"],\n\
  [\"opcao\", \"sussurrar\", \"SUSSURRAR &lt;personagem&gt; &lt;mensagem&gt;<br>Sussurra para \
outro personagem. Exemplo, SUSSURRAR JOAQUIM bom dia!\"],\n\
  [\"opcao\", \"novatos\", \"NOVATOS &lt;mensagem&gt;<br>Fala no canal de novatos.\"],\n\
  [\"opcao\", \"chat\", \"CHAT &lt;mensagem&gt;<br>Fala no canal chat.\"],\n\
  [\"opcao\", \"telepatia\", \"TELEPATIA &lt;personagem&gt; &lt;mensagem&gt;<br>Fala para um \
jogador.\"],\n\
  [\"opcao\", \"ignorar\", \"IGNORAR &lt;personagem&gt;<br>Ignora outro jogador. Se já estiver \
ignorando, esse comando deixa de ignorar.\"],\n\n\
  [\"titulo\", \"Outros\"],\n\
  [\"opcao\", \"seguir\", \"Segue alguém. Exemplo: SEGUIR CACHORRO.\"],\n\
  [\"opcao\", \"lançar\", \"LANÇAR &lt;magia&gt; [personagem]<br>Lança uma magia. Exemplo: \
LANÇAR SONO_PROFUNDO CACHORRO.\"],\n"
#
const msg_32 = "  [\"opcao\", \"matar\", \"MATAR  &lt;personagem&gt;<br>Inicia um combate. \
Exemplo: MATAR CACHORRO\"],\n\
  [\"opcao\", \"fugir\", \"Fugir seguido de um número permite fugir automaticamente. Exemplo, \
FUGIR 30 fará fugir quanto o personagem tiver menos de 30 pontos de vida.\"],\n\
  [\"envia\", \"fugir\", \"Se achar que vai morrer, pode tentar fugir do combate.\"],\n\
  [\"envia\", \"fim\", \"Sai do jogo retirando o seu personagem. Sair sem teclar FIM pode deixar \
seu personagem no jogo por alguns minutos.\"],\n];\n\n"
#
const msg_33 = "var endserv = '" + host + "';\n\n// -->\n</script>\n</body></html>\n"


classe jogohttp_usr
herda jogsock
socket j_conec # Usuário conectado
ref j_servip # Objeto jogoservip que possui esse objeto
inttempo j_ping_tempo
inttempo j_pong_tempo
txt40 j_ping_txt # Mensagem enviada no ping
txt250 j_pong_txt # Mensagem pong que deve enviar
textotxt j_recebeu_txt # Parte da mensagem recebida
const msgsala = msgsock(arg0)
const info = "h " + j_conec.ip

func msgsock
  textotxt t
  t.addfim(txtconv(arg0, "tu"))
  enquanto t.linhas
    j_conec.msg(txtconv("+" + txtvis(t.remove), "th"), 129)
  efim

func msgtodos
  ref r
  epara r = $jogohttp_usr, r, r = objdepois(r)
    r.msgsock(arg0)
  efim

func msgprompt
  txt100 lin
  refvar r = perso.persobat
  ret !r, nulo
  lin = r.pvida >= r.pvidamax ? "100" : int(intpos(100 * r.pvida / r.pvidamax))
  lin += " " + (r.pmana >= r.pmanamax ? "100" : int(intpos(100 * r.pmana / r.pmanamax)))
  lin += " " + (r.pmove >= r.pmovemax ? "100" : int(intpos(100 * r.pmove / r.pmovemax)))
  refvar exp = r.expmax
  lin += " " + (r.expatual >= exp ? "100" : int(intpos(100 * r.expatual / exp)))
  j_conec.msg(txtconv("s" + lin, "th"), 129)

func salaobj
  ref r
  epara r = $jogoserv, r, r = objdepois(r)
    arg0.addfim(r)
  efim

func ini # Objeto foi criado
# arg0 = socket
# arg1 = objeto jogoserv
# Acerta variáveis
  j_conec = arg0
  j_conec.aflooder = 1
# Checa número de conexões por IP
  indiceitem i
  ref r = i.obj("ip " + j_conec.ip)
  se !r
    r = criar("jogoservip")
    r.endereco = "ip " + j_conec.ip
  senao r.usr.total >= config:servusr
    msgsock("-Atingido limite de conexões por IP")
    apagar(este)
    ret
  senao r.entrou
    msgsock("-Entre daqui alguns segundos")
    apagar(este)
    ret
  fimse
  j_servip = r, r.usr.addfim(este)
  r.entrou = config:servtempo
  j_ping_tempo = 600
  jogsock:ini

func fim # Objeto foi apagado
  j_servip.usr.total <= 1 && j_servip.entrou <= 0 && apagar(j_servip)
  j_conec.msg(txtconv("Connection closed", "th"), 136)
  jogsock:fim

func j_ping_tempo_exec # Enviar ping de tempos em tempos
  se j_ping_txt
    j_conec.msg(txtconv("Ping timed out", "th"), 136)
    j_conec.fechar
    apagar(este)
  senao
    datahora dh
    dh.agora
    j_ping_tempo = 600
    j_ping_txt = txtconv(dh.numseg, "th")
    j_conec.msg(j_ping_txt, 137)
  fimse
# telatxt tt
# tt.msg("PING>" + j_ping_txt + "\n")

func j_pong_tempo_exec # Responder pong a um ping do navegador
  !j_conec.msg(j_pong_txt, 138) && (j_pong_tempo = 10)
# telatxt t
# t.msg("PONG>" + j_pong_txt + "\n")

func j_conec_fechou # Conexão fechou
  apagar(este)

func j_conec_msg # Recebeu mensagem do usuário
# arg0=mensagem
# telatxt tt
# tt.msg("<" + arg2 + "> " + txtconv(arg0, "ht") + "\n")
  casovar arg2
  casose "138" # Pong
    arg0 == j_ping_txt && (j_ping_txt = "")
    sair
  casose "137" # Ping
    j_pong_txt = arg0
    j_pong_tempo = 1
    sair
  casose "129" # Mensagem de texto
  casose "130" # Mensagem binária
    j_recebeu_txt.limpar
    refvar m1 = txtconv(txtconv(arg0, "ht"), "ut")
    txt(m1, 0, 1) == "+" && recebe(txtremove(txt(m1, 1), "ED"))
    sair
  casose "1" # Início de mensagem de texto
  casose "2" # Início de mensagem binária
    j_recebeu_txt.limpar
    j_recebeu_txt.addfim(arg0)
    sair
  casose "0" # Continuação da mensagem
    j_recebeu_txt.addfim(arg0)
    j_recebeu_txt.bytes >= 3000 && apagar(este)
    sair
  casose "128" # Última parte da mensagem
    refvar m1 = txtconv(txtconv(j_recebeu_txt.remove(1000) + arg0, "ht"), "ut")
    txt(m1, 0, 1) == "+" && recebe(txtremove(txt(m1, 1), "ED"))
    sair
  casose "136" # Fechar a conexão
    j_conec.msg(arg0, 136)
    j_conec.fechar
    apagar(este)
    sair
  casose # Código de mensagem desconhecido
    apagar(este)
    sair
  casofim
