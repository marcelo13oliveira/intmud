classe cmd_descbar
herda comando_comum, comando_editor
const posic = 5

func escr
  se arg0.dono.dono.var.z_dono != arg0.descnome
    ret arg0.msg("Você não está dentro de sua barraca.")
  senao
    passo(arg0.sock, "\b")
  fimse

func passo # Alterando descrição da barraca
  casovar txtmin(txt(arg1, 0, 1))
  casose "\b"
    textopos p1
    arg0.teditor.limpar
    arg0.teditor.addfim(arg0.perso.dono.dono.var.z_desc)
    arg0.teditor.bytes == 1 && arg0.teditor.limpar
    arg0.leditor = arg0.teditor.ini
    arg0.leditor1 = arg0.teditor.ini
    arg0.leditor2 = arg0.teditor.fim
    arg0.leditor2.antes
    arg0.passo = este
    arg0.msg("Editando descrição da barraca, O opções, X salvar, Z sair")
    ret 1
  casose "z" # Sair do editor
    arg0.msg("Fechando editor")
    arg0.passo = ""
    ret 1
  casose "x" # Salvar o texto
    refvar identro = arg0.perso.dono.dono.var.z_dono
    se !arg0.perso.dentrocasa && !arg0.perso.j_imortal && identro != arg0.perso.descnome
      arg0.msg("Você não está dentro da sua casa.")
    senao arg0.teditor.linhas > 8
      arg0.msg("Máximo permitido 8 linhas.")
    senao !arg0.teditor.linhas
      arg0.perso.dono.dono.var.z_desc = ""
      arg0.perso.dono.dono.objmudou = 1
      arg0.msg("Descrição da barraca apagada.")
    senao
      arg0.perso.dono.dono.var.z_desc = arg0.teditor.ini.textolin(100)
      arg0.perso.dono.dono.objmudou = 1
      arg0.msg("Descrição da barraca alterada.")
    fimse
    ret 1
  casofim
  ret passoeditor(arg0, arg1, 1000, "descrição da barraca")
