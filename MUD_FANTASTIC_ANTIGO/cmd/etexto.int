classe cmd_etexto
herda comando_comum, comando_editor
#
const posic = 5
const maquina = "i_itens100"
const folha = "i_carta"

func escr
  se !arg0.dentro1.objini(maquina)
    ret arg0.msg("Você não possui uma máquina de escrever.")
  senao !arg0.dentro1.objini(maquina).dentro1.objini(folha)
    ret arg0.msg("Coloque uma folha dentro da máquina para começar a escrever.")
  senao
    se arg1
      arg0.dentro1.objini(maquina).dentro1.objini(folha).var.z_titulo = arg1
    fimse
    $mens.p(arg0)
    $mens.mtodos2("", "$P começou a escrever em uma máquinade escrever.")
    passo(arg0.sock, "\b", arg1)
  fimse

func passo # Alterando texto
  casovar txtmin(txt(arg1, 0, 1))
  casose "\b"
    textopos p1
    arg0.teditor.limpar
    arg0.teditor.addfim(arg0.perso.dentro1.objini(maquina).dentro1.objini(folha).var.z_texto)
    arg0.teditor.bytes == 1 && arg0.teditor.limpar
    arg0.leditor = arg0.teditor.ini
    arg0.leditor1 = arg0.teditor.ini
    arg0.leditor2 = arg0.teditor.fim
    arg0.leditor2.antes
    arg0.passo = este
    arg0.msg("Editando " + arg2 + ", O opções, X salvar, Z sair")
    ret 1
  casose "z" # Sair do editor
    arg0.msg("Fechando editor")
    arg0.passo = ""
    ret 1
  casose "x" # Salvar o texto
    se arg0.teditor.linhas > 100
      arg0.msg("Máximo permitido 100 linhas.")
    senao !arg0.teditor.linhas
      arg0.perso.dentro1.objini(maquina).dentro1.objini(folha).var.z_texto = ""
      arg0.perso.dono.objmudou = 1
      arg0.msg("texto apagada.")
    senao
      arg0.perso.dentro1.objini(maquina).dentro1.objini(folha).var.z_texto = arg0.teditor.ini.tex\
tolin(100)
      arg0.perso.dentro1.objini(maquina).dentro1.objini(folha).objmudou = 1
      arg0.msg("texto alterado.")
    fimse
    ret 1
  casofim
  ret passoeditor(arg0, arg1, 5000, "texto")
