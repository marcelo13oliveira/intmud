classe cmd_gemot
herda comando_comum
const posic = 0
const txtajuda = "Gemot:\n\
Sintaxe:\n\
Digite gemot e uma mensagem, exemplo:\n\
gemot está alégre.\n\
Amensagem irá para o mud todo, com seu nome na frente, por exemplo:\n\
Ana está alégre."

func escr
  $mens.p(arg0)
  $mens.mens = arg1
  se arg2.c_gemot
    $mensjog.msg(arg2.c_gemot, arg0)
  senao arg0.pnivel < config:gemotnivel
    arg0.msg("Para escrever no canal gemot você precisa estar no nível " + config:gemotnivel + \
".")
  senao !arg0.jogconfig.24
    arg0.msg("Canal gemot está desabilitado; para habilitar tecle CONFIG +gemot")
  senao !arg1
    arg0.msg("Tecle a mensagem após gemot.")
  senao config:gemottipo == 2 # Escrever para todos
    $misclog.jogchat("gemot " + arg0.nome + ": " + arg1)
    indiceitem item
    epara item.ini("un "), txt1(item.txt) == "un", item.depois
      continuar !item.obj.jogconfig.24 || item.obj.perso.var.u_[arg0.nome]
      $mens.msg(config:gemotmsg, item.obj.perso)
    efim
  senao # Escrever para quem estiver na mesma sala
    $misclog.jogchat("gemot " + arg0.nome + ": " + arg1)
    listaobj l
    epara arg0.sock.salaobj(l), l, l.ini.remove
      continuar !l.objini.jogconfig.24 || l.objini.perso.var.u_[arg0.nome]
      $mens.msg(config:gemotmsg, l.objini.perso)
    efim
  fimse
